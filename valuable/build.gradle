apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.android.library'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.0-rc1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFile 'proguard-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    compileOnly 'com.android.support:appcompat-v7:27.1.1'
    compileOnly "com.google.code.findbugs:jsr305:3.0.2"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}


group = "com.dieyidezui.valuable"
version = android.defaultConfig.versionName

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
    }
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'dieyidezui'
                        name '耿万鹏'
                        email 'dieyidezui@gmail.com'
                    }
                }
            }
        }
    }
}


bintray {
    user = BINTRAY_USER
    key = BINTRAY_KEY
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "valuable"    //发布到JCenter上的项目名字
        websiteUrl = "https://github.com/dieyidezui/Valuable"
        vcsUrl = "git@github.com:dieyidezui/Valuable.git"
        licenses = ["Apache-2.0"]
        publish = true
    }
}